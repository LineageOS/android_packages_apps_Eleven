buildscript {
    ext {
        commonGradlePlugin = "3.3.2"

        kotlinVersion = "1.3.21"
        kotlinCoroutinesVersion = "1.1.1"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${commonGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

tasks.register("updateGradleWrapper", Wrapper) {
    // https://docs.gradle.org/current/release-notes.html
    // https://github.com/gradle/gradle/releases
    gradleVersion = '5.2.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

//==================================================================================================

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "org.lineageos.eleven"

        minSdkVersion 26
        targetSdkVersion 28

        versionCode 3
        versionName '3.0'

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"

            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
        }
        debugProguard {
            applicationIdSuffix ".dev"

            debuggable true
            proguardFiles 'proguard.cfg', 'proguard-debug.cfg'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        release {
            debuggable false
            proguardFiles 'proguard.cfg'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
    }

    lintOptions {
        baseline file("lint-baseline.xml")

        abortOnError true
        checkAllWarnings true
        showAll true

        warningsAsErrors true

        // HTML report is enough (can be found inside build/reports/)
        xmlReport false

        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'

            aidl.srcDirs = ['src']
            assets.srcDirs = ['assets']
            java.srcDirs = ['src']
            kotlin.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            resources.srcDirs = ['res']
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinCoroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlinCoroutinesVersion}"

    // AndroidX - https://developer.android.com/jetpack/androidx/releases

    // see https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation "androidx.appcompat:appcompat:1.0.2"

    // see https://developer.android.com/jetpack/androidx/releases/cardview
    implementation "androidx.cardview:cardview:1.0.0"

    // see https://developer.android.com/jetpack/androidx/releases/constraintlayout
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // see https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"

    // see https://developer.android.com/jetpack/androidx/releases/media
    implementation "androidx.media:media:1.0.1"

    // see https://developer.android.com/jetpack/androidx/releases/palette
    implementation "androidx.palette:palette:1.0.0"

    // see https://developer.android.com/jetpack/androidx/releases/preference
    implementation "androidx.preference:preference:1.0.0"

    // see https://developer.android.com/jetpack/androidx/releases/recyclerview
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // see https://developer.android.com/jetpack/androidx/releases/room
    implementation "androidx.room:room-runtime:2.0.0"
    //implementation "androidx.room:room-ktx:2.0.0"
    kapt "androidx.room:room-compiler:2.0.0"

    // see https://github.com/material-components/material-components-android/releases
    implementation "com.google.android.material:material:1.0.0"
}

